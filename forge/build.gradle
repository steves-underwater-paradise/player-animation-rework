//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "${mod_id}-common.mixins.json"
    }
}

repositories {
    maven { url "https://maven.neoforged.net/releases/" }
}

dependencies {
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // (Neo)Forge
    forge "net.neoforged:forge:${rootProject.minecraft_version}-${rootProject.neoforge_version}"

    // Architectury API
    modRuntimeOnly("dev.architectury:architectury-forge:${rootProject.architectury_api_version}")

    // Player Animator
    modImplementation("dev.kosmx.player-anim:player-animation-lib-forge:${rootProject.player_animator_version}")
}

processResources {
    inputs.property "version", project.version

    from(project(":common").sourceSets["main"].resources) {
        include("assets", "data", "resourcepacks")
    }

    filesMatching("mods.toml") {
        expand "version": project.version,
                "mod_id": rootProject.mod_id,
                "mod_name": rootProject.mod_name,
                "mod_description": rootProject.mod_description,
                "mod_license": rootProject.mod_license,
                "git_repository_host": rootProject.git_repository_host,
                "git_organization": rootProject.git_organization,
                "curseforge_project_id": rootProject.curseforge_project_id,
                "modrinth_project_id": rootProject.modrinth_project_id,
                "supported_minecraft_version": rootProject.supported_minecraft_version,
                "fabric_loader_version": rootProject.fabric_loader_version,
                "forge_version": rootProject.neoforge_version,
                "fabric_api_version": rootProject.fabric_api_version,
                "architectury_api_version": rootProject.architectury_api_version
    }
}
